// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    /**
     *  reset load inc | select
     *  ---------------+-------
     *      0    0   0 |  cur
     *      0    0   1 |  curPlusOne
     *      0    1   0 |  in
     *      0    1   1 |  in
     *      1    0   0 |  zero
     *      1    0   1 |  zero
     *      1    1   0 |  zero
     *      1    1   1 |  zero
     */
    Inc16(in=cur, out=curPlusOne);
    Mux8Way16(
        a=cur, b=curPlusOne, c=in, d=in,
        e=false, f=false, g=false, h=false,
        sel[2]=reset, sel[1]=load, sel[0]=inc, out=muxOut
    );
    Register(in=muxOut, load=true, out=cur, out=out);
}
